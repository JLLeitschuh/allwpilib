// There are two hal libraries that are built
// - Desktop hal which is used by simulation (gcc/msvc)
// - Athena hal which is used by the roborio (arm)

apply plugin: 'cpp'

model {
    components {
        HALAthena(NativeLibrarySpec) {
            targetPlatform 'arm'
            tasks.withType(CppCompile) {
                dependsOn addArmNiLibraryLinks
            }
            sources {
                cpp {
                    source {
                        srcDirs = ["lib/Athena", "lib/Athena/FRC_FPGA_ChipObject", "lib/Shared"]
                        includes = ["**/*.cpp"]
                    }
                    exportedHeaders {
                        srcDirs = ["include", "lib/Athena", "lib/Athena/FRC_FPGA_ChipObject", "lib/Shared"]
                    }
                }
            }
        }
        HALDesktop(NativeLibrarySpec) {
            targetPlatform 'desktop'
            binaries.all {
                if (toolChain in VisualCpp){
                    cppCompiler.define "FRC_SIMULATOR"
                    cppCompiler.args "/EHsc"
                }
            }
            tasks.withType(CppCompile) {
                dependsOn addDesktopNiLibraryLinks
            }
            sources {
                cpp {
                    source {
                        srcDirs = ["lib/Desktop", "lib/Shared"]
                        includes = ["**/*.cpp"]
                    }
                    exportedHeaders {
                        srcDirs = ["include", "lib/Desktop", "lib/Shared"]
                    }
                }
            }
        }
    }
}
