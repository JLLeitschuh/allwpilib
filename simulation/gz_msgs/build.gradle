//imports
import org.apache.tools.ant.filters.ReplaceTokens

//plugins
// proto files are build by the protobuf plugin
apply plugin: 'protobuf'
// used for build the share library gz_msgs.so
apply plugin: 'cpp'

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.andrewkroh.gradle:gradle-protobuf-plugin:0.4.0'
  } 
}

repositories {
  mavenCentral()
}

//describe how to build the gz_msgs library
model {
  components {
    gz_msgs(NativeLibrarySpec){
      sources {
        cpp {
          source  {
            srcDirs 'build/generated/cpp'
            include '**/*.cc'
          }
          exportedHeaders {
            srcDirs 'build/generated/cpp'
            include '**/*.h'
          }
        }
      }
    }
  }
}

tasks.whenTaskAdded { task ->
  if (task.name == 'msgs_header'){
    task.dependsOn 'compileProto'
  }
  else if (task.name == 'gz_msgsSharedLibrary') {
    task.dependsOn 'msgs_header'
  }
}

// copy msg.h.in and replace token
// this file is included by all the wpilibC++Sim files
// example: https://docs.gradle.org/current/userguide/userguide_single.html#filterOnCopy
task msgs_header(type: Copy) {
  from 'src/msgs.h.in'
  into 'build/generated/'
  //get all the generated header files and include them in msgs.h
  def headers = fileTree('build/generated/cpp/').include('*.h')
  def message_headers = ""
  headers.each {
    message_headers += "#include <$it>\n"
  }
  println message_headers
  //place into msgs.h
  filter(ReplaceTokens, tokens: [message_headers: message_headers])
  //rename the file to remove the .in
  rename( /(.+)\.in/ , '$1')
}
