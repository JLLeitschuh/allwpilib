def libPattern = /(\/|\\)lib(.+).so$/
def niLibraryArgs = []
def wpiLibraryArgs = []
def niLibraryPath = file('ni-libraries').absolutePath

def niLibraryTree = fileTree(niLibraryPath)
niLibraryTree.include '*.so'
niLibraryTree.exclude '*libwpi*.so'

niLibraryTree.each { lib ->
    def nameMatcher = (lib.path =~ libPattern)
    if (nameMatcher[0].size() > 1) {
        def name = nameMatcher[0][1]
        niLibraryArgs << '-l' + name
    }
}

def wpiLibraryTree = fileTree(niLibraryPath)
wpiLibraryTree.include '*libwpi*.so'

wpiLibraryTree.each { lib ->
    def nameMatcher = (lib.path =~ libPattern)
    if (nameMatcher[0].size() > 1) {
        def name = nameMatcher[0][1]
        wpiLibraryArgs << '-l' + name
    }
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    repositories {
        mavenCentral()
        mavenLocal()
    }
    plugins.withType(JavaPlugin).whenPluginAdded {
    }
    plugins.withType(CppPlugin).whenPluginAdded {
        model {
            buildTypes {
                debug
            }
            platforms {
                arm {
                    architecture 'arm'
                    operatingSystem 'linux'
                }
            }
            toolChains {
                gcc(Gcc) {
                    target('arm') {
                        def compilerPrefix = 'arm-frc-linux-gnueabi-'
                        cppCompiler.executable = compilerPrefix + cppCompiler.executable
                        linker.executable = compilerPrefix + linker.executable
                        assembler.executable = compilerPrefix + assembler.executable
                        cppCompiler.withArguments { args ->
                            args << '-std=c++1y' << '-Wformat=2' << '-Wall' << '-Wextra' << '-Werror' << '-pedantic'
                            args << '-Wno-psabi' << '-Wno-unused-parameter' << '-fPIC' << '-O0' << '-g3'
                        }
                    }
                }
            }
        }

        task addNiLibraryLinks << {
            binaries.all {
                linker.args << '-L' + niLibraryPath
                linker.args.addAll(niLibraryArgs)
            }
            model {
                repositories {
                    libs(PrebuiltLibraries) { libs ->
                        // Loops through all .so files in ../ni-libraries and includes them for linking
                        niLibraryTree.each { niLib ->
                            libs.create(niLib) {
                                binaries.withType(SharedLibraryBinary) {
                                    sharedLibraryFile = file(niLib.path)
                                }
                            }
                        }
                    }
                }
            }
        }

        task addWpiLibraryLinks << {
            binaries.all {
                linker.args.addAll(wpiLibraryArgs)
            }
            model {
                repositories {
                    libs(PrebuiltLibraries) { libs ->
                        // Loops through all .so files in ../ni-libraries and includes them for linking
                        wpiLibraryTree.each { niLib ->
                            libs.create(niLib) {
                                binaries.withType(SharedLibraryBinary) {
                                    sharedLibraryFile = file(niLib.path)
                                }
                            }
                        }
                    }
                }
            }
        }

        addWpiLibraryLinks.dependsOn addNiLibraryLinks
    }
}
